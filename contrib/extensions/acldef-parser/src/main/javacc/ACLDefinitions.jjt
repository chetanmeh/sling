/* JavaCC grammar for the SLING-5355 ACL definitions language
 * See https://javacc.java.net/doc/docindex.html for the JavaCC docs
 */
 
options
{
    LOOKAHEAD=3;
    STATIC=false;
}

PARSER_BEGIN(ACLDefinitions)

package org.apache.sling.acldef.parser;

import java.util.List;
import java.util.ArrayList;

import org.apache.sling.acldef.parser.operations.*;

/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

public class ACLDefinitions
{
}

PARSER_END(ACLDefinitions)

SKIP :
{
    " "
|   "\r"
|   "\t"
|   < COMMENT: "#" (~["\n"])* "\n" >
}

TOKEN:
{
    < SET: "set" >
|   < ACL: "ACL" >
|   < ON: "on" >
|   < REMOVE: "remove" >
|   < ALLOW: "allow" >
|   < DENY: "deny" >
|   < FOR: "for" >
|   < CREATE: "create" >
|   < DELETE: "delete" >
|   < SERVICE: "service" >
|   < END: "end" >
|   < USER: "user" >
|   < NODETYPES: "nodetypes" >
|   < USERNAME: (["a"-"z"] | ["A"-"Z"] | ["0"-"9"] | "_" | "-")+ >
|   < COMMA: "," >
|   < STAR: "*" >
|   < PRIVILEGE_NAME: (["a"-"z"] | ["A"-"Z"])+ ":" (["a"-"z"] | ["A"-"Z"])+ >
|   < PATH: "/" (["a"-"z"] | ["A"-"Z"] | ["0"-"9"] | ["-"] | ["_"] | ["."]) + >
|   < EOL: "\n" >
}

List<Operation> parse() :
{}
{
    { final List<Operation> result = new ArrayList<Operation>(); }
    
    ( 
        serviceUserStatement(result) 
        | pathsAclStatement(result) 
        | principalAclStatement(result) 
        | blankLine() 
    ) * 
    <EOF>
    
    { return result; }
}

void blankLine() :
{}
{
    <EOL>
}

List<String> usernamesList() :
{
    Token t = null;
    List<String> usernames = new ArrayList<String>(); 
    
}
{
    t = <USERNAME> { usernames.add(t.image); } 
    ( <COMMA> t = <USERNAME> { usernames.add(t.image); } )* 
    { return usernames; }
}

void serviceUserStatement(List<Operation> result) :
{
    Token t;
    List<String> usernames;
}
{
    (t=<CREATE> | t=<DELETE>) 
    <SERVICE> <USER> 
    usernames = usernamesList() 
    (<EOL> | <EOF>)
    
    {
        for(String username : usernames) {
            if(CREATE == t.kind) {
                result.add(new CreateServiceUser(username));
            } else {
                result.add(new DeleteServiceUser(username));
            }
        } 
    }
}

List<String> namespacedItemsList() :
{
    Token t = null;
    List<String> priv = new ArrayList<String>(); 
    
}
{
    t = <PRIVILEGE_NAME> { priv.add(t.image); } 
    ( <COMMA> t = <PRIVILEGE_NAME> { priv.add(t.image); } )* 
    { return priv; }
}

List<String> pathsList() :
{
    Token t = null;
    List<String> paths = new ArrayList<String>(); 
    
}
{
    t = <PATH> { paths.add(t.image); } 
    ( <COMMA> t = <PATH> { paths.add(t.image); } )* 
    { return paths; }
}

void pathsAclStatement(List<Operation> result) :
{
    List<String> paths;
    List<AclLine> lines = new ArrayList<AclLine>();
} 
{
    <SET> <ACL> <ON> paths = pathsList() <EOL>
    ( removeStarLine(lines) | userPrivilegesLine(lines) | blankLine() ) +
    <END> 
    ( <EOL> | <EOF> )
    
    {
        result.add(new SetAcl(paths, lines));
    }
}

void removeStarLine(List<AclLine> lines) : 
{
    List<String> usernames = null;
    List<String> paths = null;
}
{
    <REMOVE> <STAR> 
    (
        <FOR> usernames = usernamesList() { lines.add(new AclLine(AclLine.Action.REMOVE_ALL, null, usernames)); }
        | <ON> paths = pathsList()
    )     
    <EOL>
}

void userPrivilegesLine(List<AclLine> lines) :
{
    AclLine.Action action = AclLine.Action.REMOVE_ALL;
    List<String> privileges;
    List<String> usernames;
}
{
    ( 
        <REMOVE>        { action = AclLine.Action.REMOVE; }
        | ( <ALLOW>     { action = AclLine.Action.ALLOW; } )
        | ( <DENY>      { action = AclLine.Action.DENY; } )    
    ) 
    privileges = namespacedItemsList() 
    <FOR>
    usernames = usernamesList() 
    <EOL>

    {    
        lines.add(new AclLine(action, privileges, usernames)); 
    }
}

void pathPrivilegesLine(List<AclLine> lines) : 
{
    AclLine.Action action = AclLine.Action.REMOVE_ALL;
    List<String> privileges;
}
{
    ( 
        <REMOVE>        { action = AclLine.Action.REMOVE; }
        | ( <ALLOW>     { action = AclLine.Action.ALLOW; } )
        | ( <DENY>      { action = AclLine.Action.DENY; } )    
    ) 
    privileges = namespacedItemsList() 
    <ON> pathsList()
    ( <NODETYPES> namespacedItemsList() ) ?
    <EOL>
    
    {    
        lines.add(new AclLine(action, privileges, null)); 
    }
}

void principalAclStatement(List<Operation> result) :
{
    List <String> usernames;
    List<AclLine> lines = new ArrayList<AclLine>();
}
{
    <SET> <ACL> <FOR> usernames = usernamesList() <EOL>
    ( removeStarLine(lines) | pathPrivilegesLine(lines) | blankLine() ) +
    <END> 
    ( <EOL> | <EOF> )
    
    {
        result.add(new SetAcl(usernames, lines));
    }
}